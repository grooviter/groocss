/*
 * Copyright 2016-2023 The GrooCSS authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
plugins {
    id 'groovy'
    id "com.bertramlabs.asset-pipeline" version "3.4.4"
    id 'org.ajoberstar.git-publish'
}

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    assets "com.bertramlabs.plugins:asset-pipeline-core:$assets_pipeline"
    assets("com.bertramlabs.plugins:groocss-asset-pipeline:$assets_pipeline") {
        exclude group: "org.groocss", module: "groocss"
    }

    assets project(":groocss")
}

assets {
    minifyJs         = false
    minifyCss        = true
    enableSourceMaps = false
    maxThreads       = 8

    configOptions = [
        commonJs: true,
        enableES6: false,
        groocss: [
            prettyPrint: false,
            compress: true,
            noExts: false,
            convertUnderline: false
        ]
    ]

    excludes = [
        'groocss/*',
        '*.groocss',
        '**/jquery/**'
    ]
}

gitPublish {
    repoUri       = 'https://github.com/grooviter/groocss.git'
    branch        = 'gh-pages'
    commitMessage = "Released version $version"

    contents {
        from file('build/assets')
        duplicatesStrategy DuplicatesStrategy.INCLUDE
    }
}

gitPublishPush {
    dependsOn('setAuthentication')
}

task generateSite {
    dependsOn(":groocss:groovydoc", ":guide:asciidoctor", ":website:assetClean", ":website:assetCompile")
    doLast {
        copy {
            from "$rootDir/docs/guide/build/docs/asciidoc"
            into "$rootDir/docs/website/build/assets/guide"
        }
        copy {
            from "$rootDir/modules/groocss/build/docs/groovydoc"
            into "$rootDir/docs/website/build/assets/api"
        }
    }
}

task setAuthentication {
    doLast {
        System.setProperty('org.ajoberstar.grgit.auth.username', findProperty('PUBLISH_GH_TOKEN'))
    }
}