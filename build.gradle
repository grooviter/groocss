/*
 * Copyright [2016] [Adam L. Davis]
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
plugins {
    id 'org.kordamp.gradle.groovy-project' version "$kordamp"
    id 'org.jreleaser' version '1.3.1'
}

config {
    release = !version.endsWith('SNAPSHOT')

    info {
        name        = 'GrooCSS'
        vendor      = 'GrooCSS'
        description = 'Code CSS in Groovy'

        links {
            website      = 'https://grooviter.github.io/groocss/'
            issueTracker = 'https://github.com/grooviter/groocss/issues'
            scm          = 'https://github.com/grooviter/groocss.git'
        }
        people {
            person {
                id    = 'adamldavis'
                name  = 'Adam L. Davis'
                email = 'adamd@adamldavis.com'
                roles = ['developer']
            }
            person {
                id    = "mariogarcia"
                name  = "Mario Garcia"
                roles = ['developer']
            }
        }
    }
    licensing {
        licenses {
            license {
                id = 'Apache-2.0'
            }
        }
    }
}

jreleaser {
    release {
        github {
            token     = findProperty('PUBLISH_GH_TOKEN')
            overwrite = true
            changelog {
                formatted = 'ALWAYS'
                preset    = 'conventional-commits'
                hide {
                    uncategorized = true
                }
            }
            prerelease {
                enabled = version.endsWith("SNAPSHOT")
            }
        }
    }
    signing {
        active = 'NEVER'
    }
    deploy {
        maven { active = 'NEVER' }
    }
}

subprojects {
    pluginManager.withPlugin("java", {
        compileJava {
            sourceCompatibility = "1.8"
            targetCompatibility = "1.8"
        }
    })
}

allprojects {
    repositories {
        mavenCentral()
    }
}

task generateSite {
    dependsOn(
        "clean",
        ":groocss:groovydoc",
        ":guide:asciidoctor",
        ":website:assetClean",
        ":website:assetCompile"
    )

    doLast {
        copy {
            from "$rootDir/docs/guide/build/docs/asciidoc"
            into "$rootDir/docs/website/build/assets/guide"
        }
        copy {
            from "$rootDir/modules/groocss/build/docs/groovydoc"
            into "$rootDir/docs/website/build/assets/api"
        }
    }
}