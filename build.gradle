plugins {
    id 'org.kordamp.gradle.groovy-project' version "$kordamp"
}

config {
    release = (rootProject.findProperty('release') ?: false).toBoolean()

    info {
        name        = 'GrooCSS'
        vendor      = 'GrooCSS'
        description = 'Code CSS in Groovy'

        links {
            website      = 'http://www.groocss.org'
            issueTracker = 'https://github.com/adamldavis/groocss/issues'
            scm          = 'https://github.com/adamldavis/groocss.git'
        }
        people {
            person {
                id    = 'adamldavis'
                name  = 'Adam L. Davis'
                email = 'adamd@adamldavis.com'
                roles = ['developer']
            }
        }
    }
    licensing {
        licenses {
            license {
                id = 'Apache-2.0'
            }
        }
    }
}

subprojects {
    pluginManager.withPlugin("java", {
        compileJava {
            sourceCompatibility = "1.8"
            targetCompatibility = "1.8"
        }
    })
}

allprojects {
    repositories {
        mavenCentral()
    }
}

task generateSite {
    dependsOn(":groocss:groovydoc",":guide:clean",":guide:asciidoctor", ":website:assetClean", ":website:assetCompile")
    doLast {
        copy {
            from "$rootDir/docs/guide/build/docs/asciidoc"
            into "$rootDir/docs/website/build/assets/guide"
        }
        copy {
            from "$rootDir/modules/groocss/build/docs/groovydoc"
            into "$rootDir/docs/website/build/assets/api"
        }
    }
}